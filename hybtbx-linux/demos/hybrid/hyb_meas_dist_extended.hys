/* Plant model w/ measured disturbance modeled as an additional state (open-loop) 

   (C) 2005 by A. Bemporad, Dec. 12, 2005 */

SYSTEM plant_open_loop {
       
INTERFACE {
	STATE { REAL x1 [-10,10];
		REAL x2 [-10,10];
		REAL v [-1e3,1e3]; /* measured disturbance */
		}
	INPUT { REAL u [-1e3,1e3];
		}
	OUTPUT{ REAL y;
		}
}

IMPLEMENTATION {
		AUX { REAL z1,z2; 
		      BOOL switch; }
		AD  { switch = x1+x2<=0; } 

		DA  { z1 = {IF switch THEN .5*x1-.1*x2+u
		            ELSE .54-.004*x2+u };
		      z2 = {IF switch THEN .5*x1+v
		            ELSE .25*x1+2*v};  }
		      
	    CONTINUOUS { x1 = z1;
	      		 x2 = z2;
	      		 v  = v;
		}

	    OUTPUT { y = x1-x2;  }
	  }
}
