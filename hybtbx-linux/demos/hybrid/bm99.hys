/* 2x2 PWA system  - Example from the paper 

   A. Bemporad and M. Morari, ``Control of systems integrating logic, dynamics, 
   and constraints,'' Automatica, vol. 35, no. 3, pp. 407-427, 1999. 
   
   (C) 2003 by A. Bemporad, 2003 */

SYSTEM pwa {
       
INTERFACE {
	STATE { REAL x1 [-10,10];
		REAL x2 [-10,10];
		}
	INPUT { REAL u [-1.1,1.1];
		}
	OUTPUT{ REAL y;
		}
	PARAMETER { 
	  	REAL alpha = 1.0472; /* 60 deg in radiants */
        	REAL C = cos(alpha);
        	REAL S = sin(alpha);
	    	}
	}

IMPLEMENTATION {
		AUX { REAL z1,z2; 
		      BOOL sign; }
		AD  { sign = x1<=0; } 

		DA  { z1 = {IF sign THEN 0.8*(C*x1+S*x2)
		            ELSE 0.8*(C*x1-S*x2) };
		      z2 = {IF sign THEN 0.8*(-S*x1+C*x2)
		            ELSE 0.8*(S*x1+C*x2) };  }
		      
	    CONTINUOUS { x1 = z1;
	      		 x2 = z2+u; }

	    OUTPUT { y = x2;  }
	  }
}
